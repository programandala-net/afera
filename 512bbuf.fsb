.( 512B/BUF )

\ 512bbuf.fsb
\ 512-byte disk buffers for ZX Spectrum Abersoft Forth

\ Copyright (C) 2015 Marcos Cruz (programandala.net)

\ This file is part of
\ Afera (Abersoft Forth Extensions, Resources and Addons)
\ http://programandala.net/en.program.afera.html

  \ -----------------------------------------------------------
  \ Description

  \ This program configures Abersoft Forth to use 2 512-byte
  \ disk buffers instead of 8 128-byte disk buffers.

  \ The disk buffers of Abersoft Forth follow the fig-Forth
  \ model:

  \ B/BUF = 128 bytes per buffer
  \ B/SCR = 8 blocks per screen
  \ #BUFF = 8 disk buffers

  \ But, in order to use G+DOS disks as mass storage, with
  \ 512-byte sectors, this is more efficient, and doesn't
  \ increase the space used by the buffers:

  \ B/BUF = 512 bytes per buffer
  \ B/SCR = 2 blocks per screen
  \ #BUFF = 2 disk buffers

  \ In order to make this change, those constants have to be
  \ updated, and two words must be patched: `EMPTY-BUFFERS` and
  \ `+BUF`:

  \ The ordinary definition of fig-Forth `EMPTY-BUFFERS` is:
  \
  \ : EMPTY-BUFFERS  ( -- )
  \   FIRST LIMIT OVER - ERASE  ;

  \ But the definition implemented by Abersoft Forth is:

  \ : EMPTY-BUFFERS  ( -- )
  \   FIRST LIMIT OVER - ERASE
  \   LIMIT FIRST DO
  \     32767 I !
  \   132 +LOOP  ;

  \ The loop sets the disk block number of every buffer to
  \ 32767 (bit 16, the update bit, is set to 0).
  \
  \ The problem is the loop step, the original length of a disk
  \ buffer (132) is hardcoded, not calculated (`B/BUF 4 +`).

  \ Also the definition of `+BUF` must be patched for the same
  \ reason:

  \ : +BUF  ( a1 -- a2 f )
  \   132 DUP LIMIT = IF  DROP FIRST  THEN  DUP PREV @ -  ;

  \ -----------------------------------------------------------
  \ History

  \ 2015-05-08: Start, based on the code of the unfinished
  \ module <1024bbuf.fsb>.
  \
  \ 2015-05-09: Bug fixed. First working version.
  \
  \ 2015-05-10: `QUIT` used. No way yet to compile other
  \ modules after this one.

: 512B/BUF  ( -- )

  \ Change the constants:
  512 ' B/BUF !  2 ' B/SCR !  2 ' #BUFF !
  FIRST B/BUF 4 + #BUFF * + ' LIMIT !

  \ Patch `EMPTY-BUFFERS` and `+BUF`
  \ with the new value of `B/BUF`:
  B/BUF 4 + ' EMPTY-BUFFERS 26 + !
  B/BUF 4 + ' +BUF 2 + !

  \ Init the whole thing:
  EMPTY-BUFFERS  FIRST PREV !  FIRST USE !

  \ Make this word to forget itself:
  [ LATEST ] LITERAL DUP DP ! PFA LFA @ CURRENT @ !

  \ Done and quit:
  CR ." 2 512-byte buffers installed." QUIT

  ;  512B/BUF

  \ vim: filetype=abersoftforth
