.( 48K? 128K? )

\ 48kq.fsb
\ Memory size checks for ZX Spectrum's Abersoft Forth
\ and related system patches

\ Copyright (C) 2015 Marcos Cruz (programandala.net)

\ This file is part of
\ Afera (Abersoft Forth Extensions, Resources and Addons)
\ http://programandala.net/en.program.afera.html

  \ -----------------------------------------------------------
  \ Requirements

  \ <bank.fsb>

  \ -----------------------------------------------------------
  \ History

  \ 2015-04-07: Start. Code adapted from Lennart Benschop's
  \ Spectrum Forth-83.
  \ 
  \ 2015-05-07: Code moved to its own file, separated from
  \ `BANK`.
  \
  \ 2015-05-11: New: `CPUK`, `.CPUK` and patch for `.CPU`.

  \ -----------------------------------------------------------

HEX

: 48K?  (  -- f )
  \ Running on a 48K Spectrum?
  0 BANK FFFE @ 0 FFFE !  1 BANK FFFE @ 1 FFFE !
  0 BANK FFFE @
  >R  1 BANK FFFE !  0 BANK FFFE !  R>  ; \ restore

: 128K?  (  -- f )  48K? 0=  ;

DECIMAL  -->

( CPUK .CPUK )

: CPUK  ( -- n )
  \ KiB of the machine.
  48 128K? 80 * + ;

: .CPUK  ( -- )
  \ Print the KiB of the CPU.
  SPACE CPUK . ." KiB"  ;

  \ -----------------------------------------------------------
  \ Patch `.CPU`

  \ The original word `.CPU` prints "48K SPECTRUM", so it has
  \ to be patched. The length of the patch is the same than the
  \ original code. The patched text is stored letter by letter,
  \ thus this module remains independent from the strings
  \ module.

HEX

0B 75D6 C!  \ string length

  \ String "ZX Spectrum":
5A 75D7 C!  58 75D8 C!  BL 75D9 C!  53 75DA C!  70 75DB C!
65 75DC C!  63 75DD C!  74 75DE C!  72 75DF C!  75 75E0 C!
6D 75E1 C!

  \ At the end call `.CPUK`:
  ' .CPUK CFA 75E2 !

  \ -----------------------------------------------------------
  \ Patch `ABORT`

  \ Also `ABORT` needs a little patch:
  \ change the order of `.CPU` and `CR`:

  ' .CPU CFA 6D62 !  ' CR CFA 6D64 !

  \ -----------------------------------------------------------
  \ Protect the patches

  \ System words have been patched with new words. Those new
  \ words should not be removed, e.g. by `COLD`, otherwise the
  \ system would crash when their space will be overwritten. So
  \ the dictionary must be protected at this point:

HERE FENCE !

DECIMAL

  \ vim: filetype=abersoftforth
