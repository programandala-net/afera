.( Tape extensions )

\ tape.fsb
\ Tape extensions for ZX Spectrum Abersoft Forth.

\ Copyright (C) 2015 Marcos Cruz (programandala.net)

\ This file is part of
\ Afera (Abersoft Forth Extensions, Resources and Addons)
\ http://programandala.net/en.program.afera.html

  \ -----------------------------------------------------------
  \ Description

  \ Abersoft Forth only includes two tape words: `LOADT` and
  \ `SAVET`, that load and save its 11-KiB RAM-disk with the
  \ name "DISC".
  \
  \ No word is provided to load or save any ordinary code file,
  \ any name and size.  Loading files is especially useful in
  \ order to include graphics during the compilation of a Forth
  \ program.
  \
  \ This module defines `>TAPE` ("to tape") and `<TAPE` ("from
  \ tape"). The approach used was to patch the tape load header
  \ of Abersoft Forth, passed to the ROM routine, and restore
  \ its default values after the operation. Two more words,
  \ `/LOADT` and `/SAVET`, let improved manipulation of the RAM
  \ disk.
  
  \ -----------------------------------------------------------
  \ Requirements

  NEEDS   S"        strings

  \ -----------------------------------------------------------
  \ History

  \ 2015-03-26: Start.
  \
  \ 2015-03-28: First working version.
  \
  \ 2015-04-23: Some comments are improved.
  \
  \ 2015-04-30: `'BLOCK` instead of `BLOCK>A`, after the change
  \ in the main file of the library.
  \
  \ 2015-05-03: Simpler word names: no "MEM" affix.
  \
  \ 2015-05-08: Some words are renamed.
  \
  \ 2015-05-10: `INCLUDET` is removed; not useful with tapes.
  \ `'SCR` instead of `'BLOCK`, after the change in the main
  \ file of the library.
  \
  \ 2015-05-12: Change: `'SCR` renamed to `DISC-SCR`, after the
  \ changes in the library.
  \
  \ 2015-05-17: `NEEDS` used.  Some renamings, after the
  \ changes in the library (common core words for some tape and
  \ disk operations).

  \ -----------------------------------------------------------
  \ Documentation

  \ Dictionary references from Don Thomasson's book
  \ _Advanced Spectrum Forth_:
  \
  \ 340 (page 119): (TAPE)
  \ 
  \ pop hl
  \ push bc
  \ push ix
  \ ld a,l ; 1=LOAD, 0=SAVE
  \ ; this instruction is at 0x7618:
  \ ld hl,D000  ; start address to save or load
  \ ld ix,start-of-header-area
  \ ; ld a,(5C72) ; XXX this is a mistake in the book
  \ ; XXX fixed:
  \ ld (5C74),A
  \ call 075A
  \ pop ix
  \ pop bc
  \ jp next1
  \
  \ 344 (page 120): LOADT
  \
  \ 1 (TAPE)

  \ 345 (page 120): SAVET
  \
  \ 0 (TAPE)

  \ The load header (pointed by IX before calling the ROM
  \ routine) is at address 30182 (0x75E6).  Its contents are:

  \ +00 : 3 (identifier of code file type)
  \ +01 : "DISC      " = 10-char filename, padded with spaces
  \ +11 : 11263 (0x2BFF)  = length
  \ +13 : 53248 (0xD0FF)  = start address
  \ +15 : "  " (2 spaces) = not used for code files

  \ The save header (pointed by the ROM routine, by adding 0x11
  \ to IX) is right after the save header, at address 30199
  \ (0x75F7).

  \ -----------------------------------------------------------

2 5 THRU

( Headers )

30182 CONSTANT LOADT-HEADER \ load header
17 CONSTANT /T-HEADER  \ header length
  \ XXX OLD -- Save header, not needed.
  \ LOADT-HEADER /T-HEADER + CONSTANT SAVET-HEADER
/T-HEADER 2 * CONSTANT /T-HEADERS

10 CONSTANT /T-FILENAME \ filename max length

: >T-FILENAME  ( a1 -- a2 )   1+   ;
: >T-LENGTH    ( a1 -- a2 )  11 +  ;
: >T-START     ( a1 -- a2 )  13 +  ;

  \ Keep a copy of both tape headers:
0 VARIABLE T-HEADERS-BACKUP /T-HEADERS 2 - ALLOT
LOADT-HEADER T-HEADERS-BACKUP /T-HEADERS CMOVE

( Tools )

  \ Address of the file start address
   \ in the code of the word `(TAPE)`:
30233 CONSTANT (TAPE)-START

: T-RESTORE  ( -- )
  \ Restore the original contents of both tape headers:
  T-HEADERS-BACKUP LOADT-HEADER /T-HEADERS CMOVE
  \ Unpatch `(TAPE)`
  LO (TAPE)-START !  ;

: -T-FILENAME  ( -- )
  \ Blank the filename of the load header.
  LOADT-HEADER >T-FILENAME /T-FILENAME BLANKS ;

  \  XXX TODO use the file execution table
  \  to share one word with the disk support?
: T-FILENAME!  ( ca len -- )
  \ Store a filename into the load header.
  -T-FILENAME  /T-FILENAME MIN
  LOADT-HEADER >T-FILENAME SWAP CMOVE  ;

: ANY-T-FILENAME  ( -- )
  \ Configure the load header to load any filename.
  255 LOADT-HEADER >T-FILENAME C!  ;

: T-LENGTH!  ( len -- )
  \ Set the given code length for the next tape loading,
  \ storing it into the load header.
  LOADT-HEADER >T-LENGTH !  ;

: T-START!  ( a -- )
  \ Set the given code start for the next tape loading,
  \ storing it into the load header and patching
  \ the code of the word `(TAPE)`.
  DUP (TAPE)-START !  LOADT-HEADER >T-START !  ;

  \ ( Debugging tools )

  \ XXX TMP for debugging:

  \ : T-DUMP  ( -- )
  \   CR ." Load header:"
  \   LOADT-HEADER /T-HEADER DUMP SPACE KEY DROP
  \   CR ." Save header:"
  \   SAVET-HEADER /T-HEADER DUMP
  \   CR ." (TAPE) start: " (TAPE)-START @ U.  ;

( <TAPE-FILE >TAPE-FILE )

: <TAPE-FILE  ( a1 len1 ca2 len2 -- )
  \ Load a file from tape.
  \ a1       = destination
  \ len1     = length (or zero if unspecified)
  \ ca2 len2 = filename (len2 is zero if unspecified) 
  DUP   \ filename specified?
  IF  T-FILENAME!  ELSE  2DROP ANY-T-FILENAME  THEN
  T-LENGTH! T-START!  1 (TAPE)  T-RESTORE   ;

' <TAPE-FILE CFA '<FILE ,  \ Update the execution table.

: >TAPE-FILE  ( a1 len1 ca2 len2 -- )
  \ Save a memory region into a tape file.
  \ a1       = start address
  \ len1     = length
  \ ca2 len2 = filename
  T-FILENAME! T-LENGTH! T-START!  0 (TAPE)  T-RESTORE  ;

' >TAPE-FILE CFA '>FILE ,  \ Update the execution table.

( /LOADT /SAVET )

: /LOADT  ( n -- )
  \ Load the Forth RAM-disk from tape, at address of screen n.
  \ Warning: the file will be loaded, no matter its length.
  \ n = first screen
  EMPTY-BUFFERS DISC-SCR 0 S" DISC" <TAPE  ;

: /SAVET  ( n1 n2 -- )
  \ Save the Forth RAM-disk to tape,
  \ from screen n1 to screen n2.
  \ n1 = first screen
  \ n2 = last screen
  FLUSH 1+ DISC-SCR SWAP DISC-SCR
  DUP ROT SWAP -  S" DISC" >TAPE  ;

  \ vim: filetype=abersoftforthafera
