.( Random number generator )

\ random.fsb
\ Random number generator for ZX Spectrum Abersoft Forth

\ Copyright (C) 2015 Marcos Cruz (programandala.net)

\ This file is part of
\ Afera (Abersoft Forth Extensions, Resources and Addons)
\ http://programandala.net/en.program.afera.html

  \ -----------------------------------------------------------
  \ History of this file

  \ 2015-04-02: Start, with the code of `RND`, moved from the
  \ main file of the library.
  \
  \ 2015-04-16: Reference of the Leo Brodie's implementation.
  \ 
  \ 2015-05-17: Reference of the Matteo Vitturi's
  \ implementation.


  \ -----------------------------------------------------------

  \ The code of `RND` was adapted from Bertie, the demo program
  \ bundled with Abersoft Forth:

23670 CONSTANT SYS-SEED

: RND  ( u1 -- u2 )
  SYS-SEED @ 75 U* 75 0 D+
  OVER OVER U< - - 1 - DUP SYS-SEED !
  U* SWAP DROP ;

;S

.( Leo Brodie )

  \ From Brodie's book,
  \ for reference,
  \ in an old message from comp.lang.forth:

VARIABLE RND    HERE RND !
: RANDOM ( -- n ) RND @   31421 *  6927 +   DUP  RND ! ;
: CHOOSE ( u1 -- u2 )   RANDOM UM*   SWAP DROP  ;

  \ ____ Quote:
  \ So I assume that what you really want is UM* . That is a
  \ primitive that most Forth's have. I know MAXforth does. And
  \ Brodie's code works there.  Didacticism: * is either a
  \ signed or unsigned operator depending how you interpret the
  \ result.  UM* specifies unsigned beacuse, with mixed
  \ arithmetic, it matters. (The sign of the result might not
  \ be what you expect if the operands are negative.) Stack
  \ Diag for UM* : ( u u -- d) Email me if i missed the point.
  \ Jerry
  \ ____

\ 2015-05-16: Adapted.

HERE SYS-SEED !
: RANDOM  ( -- n )
  SYS-SEED @ 31421 * 6927 +  DUP SYS-SEED !  ;
: CHOOSE  ( u1 -- u2 )
  RANDOM U* SWAP DROP  ;

( Matteo Vitturi's vForth )

\ 2015-05-17: From Matteo Vitturi's vForth 1.3 (1990-2000).

23670 CONSTANT SYS-SEED
: RND
  1+ 8195 23672 @ U* 1 0 D+
  16383 U/ DROP DUP 23672 !
  SWAP MOD ;
: SEED  ( n -- ) 23672 ! ;

  \ vim: filetype=abersoftforth
