.( Random number generator )

\ random.fsb
\ Random number generator for ZX Spectrum Abersoft Forth

\ Copyright (C) 2015 Marcos Cruz (programandala.net)

\ This file is part of
\ Afera (Abersoft Forth Extensions, Resources and Addons)
\ http://programandala.net/en.program.afera.html

  \ Copying and distribution of this file, with or without
  \ modification, are permitted in any medium without royalty
  \ provided the copyright notice and this notice are
  \ preserved.  This file is offered as-is, without any
  \ warranty.

  \ -----------------------------------------------------------
  \ History

  \ 2015-04-02: Start, with the code of `RND`, moved from the
  \ main file of the library.
  \
  \ 2015-04-16: Reference of the Leo Brodie's implementation.
  \
  \ 2015-05-17: Reference of the Matteo Vitturi's
  \ implementation.
  \
  \ 2015-07-04: The version based on Leo Brodie's code is the
  \ default one.
  \
  \ 2015-10-26: Updated copyright notices and comments. Fixed
  \ the default `RANDOM`.

  \ -----------------------------------------------------------

  \ Code adapted from Leo Brodie's "Starting Forth" (second
  \ edition, 1987, chapter 10, page 235), and Gforth's
  \ <random.fs>.

23670 CONSTANT SYS-SEED

: RND  ( -- n )
  SYS-SEED @ 31421 * 6927 +  DUP SYS-SEED !  ;

: RANDOM  ( u -- 0..u-1 )  RND U* NIP  ;

: RANDOMIZE  ( n -- )  SYS-SEED !  ;

( Alternative: RND of Abersoft Forth )

  \ Code from "Bertie", the demo program bundled with Abersoft
  \ Forth (1983).

  \ Copyright (C) 1983 John Jones-Steele

23670 CONSTANT SYS-SEED

: RND  ( u1 -- u2 )
  SYS-SEED @ 75 U* 75 0 D+
  OVER OVER U< - - 1 - DUP SYS-SEED !
  U* NIP ;

( Alternative: RND of vForth )

  \ Code from Matteo Vitturi's vForth 1.3 (1990-2015).

  \ Copyright (C) 1990-2015 Matteo Vitturi

23670 CONSTANT SYS-SEED

: RND  ( n1 -- n2 )
  1+ 8195 23672 @ U* 1 0 D+
  16383 U/ DROP DUP SYS-SEED !
  SWAP MOD ;

: SEED  ( n -- ) SYS-SEED ! ;

  \ vim: filetype=abersoftforthafera
