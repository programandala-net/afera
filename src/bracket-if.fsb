.( [IF] [ELSE] [THEN] )

\ bracket-if.fsb
\ Conditional compilation for ZX Spectrum Abersoft Forth

\ Copyright (C) 2015 Marcos Cruz (programandala.net)

\ This file is part of
\ Afera (Abersoft Forth Extensions, Resources and Addons)
\ http://programandala.net/en.program.afera.html

  \ Copying and distribution of this file, with or without
  \ modification, are permitted in any medium without royalty
  \ provided the copyright notice and this notice are
  \ preserved.  This file is offered as-is, without any
  \ warranty.


  \ -----------------------------------------------------------
  \ History

  \ 2015-03-28: Start, with code adapted from Forth 5mx
  \ (http://programandala.net/en.program.forth_5mx.html), that
  \ was already adapted from pForth.
  \
  \ 2015-04-08: Changes.
  \
  \ 2015-04-27: New: `?-->` and `?;S`, as a simple alternative
  \ to the unfinished `[IF]`.

  \ 2015-05-02: Change: `[DEFINED]`, `[UNDEFINED]`, `?-->` and
  \ `?;S` are moved to the main file of the library.
  \
  \ 2015-05-15: New version, adapted from Albert van der
  \ Horst's lina Forth.
  \
  \ 2015-05-17: `NEEDS` used.
  \
  \ 2015-06-04: `STR=` updated to `S=`.
  \
  \ 2015-10-30: Updated license.
  \
  \ 2015-11-18: Updated license.

  \ -----------------------------------------------------------
  \ Requirements

  NEEDS S"          strings
  NEEDS S=          s-equals
  NEEDS PARSE-NAME  strings

-->

( [IF] [ELSE] [THEN] )

: [ELSE]  ( "..." -- )

  1 BEGIN   PARSE-NAME 2DUP SWAP C@ AND
    WHILE   2DUP S" [IF]" S=
            IF    2DROP 1+
            ELSE  2DUP S" [ELSE]" S=
                  IF    2DROP 1- DUP IF  1+  THEN
                  ELSE  S" [THEN]" S= IF  1-  THEN
                  THEN
            THEN  -DUP 0= IF  EXIT  THEN
  REPEAT  2DROP DROP  ; IMMEDIATE

: [IF]  ( "..." -- )  0= IF [COMPILE] [ELSE] THEN  ; IMMEDIATE

: [THEN]  ( -- )  ; IMMEDIATE

  \ vim: filetype=abersoftforthafera
