.( TRANSIENT )
\ transient.fsb
\ Transient code for ZX Spectrum Abersoft Forth

\ Copyright (C) 2015 Marcos Cruz (programandala.net)
\ Copyright (C) 1988 Lennart Benschop

\ This file is part of
\ Afera (Abersoft Forth Extensions, Resources and Addons)
\ http://programandala.net/en.program.afera.html

  \ Copying and distribution of this file, with or without
  \ modification, are permitted in any medium without royalty
  \ provided the copyright notice and this notice are
  \ preserved.  This file is offered as-is, without any
  \ warranty.

  \ -----------------------------------------------------------
  \ History

  \ 2015-03-15: Start. Code adapted from the <TASM> file of
  \ Lennart Benschop's Spectrum Forth-83 (1988).
  \
  \ 2015-05-01: Rewritten. New approach to make it a generic
  \ tool.
  \
  \ 2015-05-08: Improved to work also when the system has been
  \ lowered below 0xC000 by the module <lowersys.fsb>. 
  \
  \ 2015-05-09: The unlinking done by `-TRANSIENT` was fixed
  \ and adapted to fig-Forth with the help of Lennart Benschop.

  \ -----------------------------------------------------------

-->

( TRANSIENT -- variables )

0 VARIABLE OLD-DP
0 VARIABLE OLD-LATEST
0 VARIABLE OLD-VOC-LINK

-->

( TRANSIENT -- main )

: TRANSIENT[  ( u -- )

  \ Start transient code, reserving u bytes for it (including
  \ dictionary space and data stack).
  \
  \ This word must be used before compiling the transient code.
  \ The compiled size of the transient code must be known in
  \ advance, and sum say 128 bytes to it for the data stack.
  \
  \ Note: Adding data stack space to u is unnecessary when the
  \ system has been lowered below 0xC000 by the <lowersys.fsb>
  \ module.

  HERE        OLD-DP !
  LATEST      OLD-LATEST !
  VOC-LINK @  OLD-VOC-LINK !

  \ The free dictionary space top limit is `SP@` in the
  \ standard memory map. But when the system has been lowered
  \ by the module <lowersys.fsb>, it's `LO` instead (the start
  \ of RAM-disk). And when the system uses 16-KiB RAM-disks,
  \ the limit is the top of the memory.  `FREE` is used to get
  \ the current value, because the first word compiled in its
  \ pfa returns the top address `SP@`, `LO` or `0`).

  ' FREE @ EXECUTE

  SWAP - DP !  ;

: ]TRANSIENT  ( -- )

  \ End the transient code.
  \
  \ This word must be used after compiling the transient code.

  OLD-DP @ DP !  ;

: -TRANSIENT  ( -- )

  \ Remove the transient code, unlinking the dictionary space
  \ that was reserved for it.
  \
  \ This word must be used when the transient code is not going
  \ to be used any more.

  OLD-VOC-LINK @ VOC-LINK !

  \ Store the nfa of the latest word created
  \ before compiling the transient code,
  \ into the lfa of the first word created
  \ after the transient code was finished
  \ by `]TRANSIENT`.

  OLD-LATEST @ OLD-DP @ PFA LFA !  ;

  \ vim: filetype=abersoftforthafera
