\ time.fsb
\ Time extensions for ZX Spectrum Abersoft Forth.

\ Copyright (C) 2015 Marcos Cruz (programandala.net)

\ This file is part of
\ Afera (Abersoft Forth Extensions, Resources and Addons)
\ http://programandala.net/en.program.afera.html

  \ -----------------------------------------------------------
  \ History

  \ 2015-04-02: Start, with the code of `MS`, moved from the
  \ main file of the library.
  \
  \ 2015-04-03: New: `TIME@`, `TIME!` and `TIME0`.

-->

.( Time extensions )

FORTH DEFINITIONS  DECIMAL

23672 CONSTANT SYS-FRAMES

: MS  ( n -- )
  \ Wait n ms (miliseconds), with 20 ms precision.
  20 / SYS-FRAMES @ +
  BEGIN  DUP SYS-FRAMES @ U<  UNTIL DROP  ;

: TIME@  ( -- d )
  \ System frames counter (incremented every 20 ms).
  SYS-FRAMES @ [ SYS-FRAMES 2+ ] LITERAL C@  ;

: TIME!  ( d -- )
  \ Set the system frames counter.
  [ SYS-FRAMES 2+ ] LITERAL C! SYS-FRAMES !  ;

: TIME0  ( -- )
  \ Reset the system frames counter.
  0. TIME!  ;

  \ vim: filetype=abersoftforth
