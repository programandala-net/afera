.( DUMP )

\ dump.fsb
\ `DUMP` for ZX Spectrum Abersoft Forth

\ Copyright (C) 2015 Marcos Cruz (programandala.net)
\ Copyright (C) 1988 Lennart Benschop

\ This file is part of
\ Afera (Abersoft Forth Extensions, Resources and Addons)
\ http://programandala.net/en.program.afera.html

  \ Copying and distribution of this file, with or without
  \ modification, are permitted in any medium without royalty
  \ provided the copyright notice and this notice are
  \ preserved.  This file is offered as-is, without any
  \ warranty.

  \ -----------------------------------------------------------
  \ Requirements

  NEEDS CSWAP cswap

  \ -----------------------------------------------------------
  \ History

  \ 2015-03-26: Start. Copied from Lennart Benschop's Spectrum
  \ Forth-83.
  \
  \ 2015-03-28: Simple version as a temporary alternative,
  \ until `?DO` is defined.
  \
  \ 2015-04-17: The code from ZX Spectrum Forth-83 has been
  \ adapted, with `IF ... DO` instead of `?DO`.
  \
  \ 2015-04-20: Fix in `H.`.
  \
  \ 2015-05-17: `NEEDS` used.

  \ -----------------------------------------------------------

-->

( BS H. STYPE )

: BS  ( -- )  8 EMIT  ;

: H.  ( n -- )
  \ Print n in hexadecimal with four digits.
  BASE @ HEX SWAP S->D <# # # # # #> TYPE SPACE BASE !  ;

HEX

: STYPE  ( ca len -- )
  0 2DUP - IF
    DO
      DUP C@ 7F AND DUP BL < IF  DROP [CHAR] .  THEN  EMIT 1+
    LOOP
  ELSE  2DROP  THEN  DROP  ;

DECIMAL  -->

( DUMP )

: DUMP  ( a len -- )
  7 + -8 AND 8 / 0
  2DUP - IF
    DO
      CR DUP H.
      8 0 DO
        I OVER + @ CSWAP H.
      2 +LOOP
      DUP BS 8 STYPE
      ?TERMINAL IF  LEAVE  THEN
    8 + LOOP
  ELSE  2DROP  THEN  DROP  ;

  \ vim: filetype=abersoftforthafera
