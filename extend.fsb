\ extend.fsb
\ Main extensions for ZX Spectrum Abersoft Forth

\ Copyright (C) 1985-1987,2015 Marcos Cruz (programandala.net)

\ This file is part of
\ Afera (Abersoft Forth Extensions, Resources and Addons)
\ http://programandala.net/en.program.afera.html

  \ History of this file: see at the end of the file.

( Line comments, booleans, bug fixes)

." Main extensions "

FORTH DEFINITIONS

  \ ............................................
  \ Line comments

: \  ( "ccc<newline>" -- )
  IN @ DUP C/L > IF  B/BUF SWAP MOD  ELSE   C/L SWAP -  THEN
  IN +!  ; IMMEDIATE

: .(  ( "ccc<paren>" -- )
  41 TEXT PAD COUNT TYPE  ; IMMEDIATE

  \ `.(` could be defined this way, but the strings module
  \ would be required:
  \
  \ : .(  ( "ccc<paren>" -- )
  \   [CHAR] ) PARSE-TEXT TYPE  ; IMMEDIATE

  \ ............................................
  \ Booleans

0 CONSTANT FALSE  FALSE 0= CONSTANT TRUE

: ON   ( a -- )  TRUE  SWAP !  ;
: OFF  ( a -- )  FALSE SWAP !  ;

  \ ............................................
  \ Bugs found in Abersoft Forth
  \ during the development of Afera

  \ The length of the RAM disk must be 11264 (0x2C00),
  \ not 11263 (0x2BFF):

11264 CONSTANT /RAM-DISC      \ length of the RAM disk
/RAM-DISC 30182 11 + !        \ patch the load tape header
/RAM-DISC ' INIT-DISC 6 + !   \ patch `INIT-DISC`

 \ `EXIT` must do `R> DROP`, not `>R DROP`:

 ' R> CFA ' EXIT !

  \ Bug not fixed yet: `MESSAGE` does not work with negative
  \ values.

-->

( Flow control)

  \ Compile a call to the word being defined.
: RECURSE  ( -- )  LATEST PFA CFA ,  ; IMMEDIATE

  \ Exit the current word if the given flag is not zero.
: ?EXIT  ( f -- )
  [COMPILE] IF  COMPILE EXIT  [COMPILE] THEN  ; IMMEDIATE

: BYE  ( -- )  MON  ;

-->

( Operators) 

: CHAR  ( "name" -- c )  BL WORD HERE 1+ C@  ;
: [CHAR]  ( "name" -- c )  CHAR [COMPILE] LITERAL  ; IMMEDIATE

: INVERT  ( n1 -- n2 )  -1 XOR  ;
: BOUNDS  ( a1 len1 -- a2 a1 )  OVER + SWAP  ;
: >=  ( n1 n2 -- f )  SWAP < 0=  ;
: <=  ( n1 n2 -- f )  SWAP > 0=  ;
  \ `-` is faster than `<>`, but does not return `TRUE`:
: <>  ( n1 n2 -- f )  = 0=  ;   
: SGN  ( n1 -- -1|0|1 )  DUP IF  0< 2 * 1+  THEN  ;

-->

( 2* 2- 1- )

HEX 

CREATE 2*  ( n1 -- n2 )
  \ DUP +
  E1 C, 29 C,        \ pop hl / add hl,hl
  C3 C, PUSHHL ,     \ jp PUSHHL
  SMUDGE

CREATE 2-  ( n1 -- n2 )
  E1 C, 2B C, 2B C,  \ pop hl / dec hl / dec hl
  C3 C, PUSHHL ,     \ jp PUSHHL
  SMUDGE

CREATE 1-  ( n1 -- n2 )
  E1 C, 2B C,         \ pop hl / dec hl
  C3 C, PUSHHL ,      \ jp PUSHHL
  SMUDGE

-->

( 2/ )

CREATE 2/  ( n1 -- n2 )

  \ Adapted from the `PICK` written on 1985-05-07 by Edmund
  \ Ramm (P.O.Box 38, 2358 Kaltenkirchen, Fed. Rep. of Germany)
  \ for the Z80 fig-Forth implementation written by Dennis L.
  \ Wilson (1980-09-07).

  \ XXX FIXME does not work fine with negative numbers

  E1 C,           \ pop hl
  CB C, 7C C,     \ bit 7,h ; negative?
  18 C, 03 C,     \ jr z,twosl1 ; no
  23 C,           \ inc hl ; yes, add 1
  \ twosl1:
  CB C, 2C C,     \ sra h 
  CB C, 1D C,     \ rr l ; asr hl
  C3 C, PUSHHL ,  \ jp PUSHHL
  SMUDGE

-->

( Data stack)

CREATE PICK  ( u -- x )

  \ 2 * 2 + SP@ + @

  \ Adapted from the `PICK` written on 1985-05-07 by Edmund
  \ Ramm (P.O.Box 38, 2358 Kaltenkirchen, Fed. Rep. of Germany)
  \ for the Z80 fig-Forth implementation written by Dennis L.
  \ Wilson (1980-09-07).
 
  E1 C, 29 C,          \ pop hl / add hl,hl
  39 C,                \ add hl,sp  ; offset into stack
  5E C, 23 C, 56 C,    \ ld e,(hl) / inc hl / ld d,(hl)
  EB C, C3 C, PUSHHL , \ ex de, hl / jp PUSHHL
  SMUDGE

CREATE NIP  ( n1 n2 -- n2 )
  \ SWAP DROP
  E1 C, D1 C,        \ pop hl / pop de
  C3 C, PUSHHL ,     \ jp PUSHHL
  SMUDGE

CREATE TUCK  ( n1 n2 -- n2 n1 n2 )
  \ SWAP OVER
  E1 C, D1 C, E5 C,  \ pop hl / pop de / push hl
  C3 C, PUSHDE ,     \ jp PUSHDE
  SMUDGE 

DECIMAL -->

( Data stack)

: -ROT  ( x1 x2 x3 -- x3 x1 x2 )  ROT ROT  ;
  
  \ XXX TODO -- `2>R` and `2R>` in assembler

  \ XXX OLD -- unfinished version
  \ : ROLL  ( u -- )
  \ 2 * 4 + DUP SP@ + DUP @ ROT 1 - DUP 2 + ROT 2 -
  \ CMOVE DROP  ;
: ROLL  ( u -- )
  DUP 1 < IF  DROP  ELSE  SWAP >R 1 - RECURSE R> SWAP THEN  ;

: DEPTH  ( -- u )  SP@ S0 @ - -2 /  ;

-->

( Blocks)

  \ ............................................
  \ Standard or common usage extensions

: THRU  ( n1 n2 -- )  1+ SWAP DO  I LOAD  LOOP  ;
: +LOAD  ( n -- )  BLK @ B/SCR / + LOAD  ;
: +THRU  ( n1 n2 -- )  1+ SWAP DO  I +LOAD  LOOP  ;

  \ ............................................
  \ Specific extensions

B/SCR B/BUF * CONSTANT /BLOCK  \ bytes per block

: /INIT-DISC  ( n -- )
  \ Blank the init disc from the given block number.
  /BLOCK * DUP >R /RAM-DISC SWAP - LO R> + SWAP BLANKS  ;

: BLOCK>A  ( block -- a )
  \ Convert a block number (0..10) to its address in the
  \ Forth RAM-disc.
  /BLOCK * LO +  ;

  \ -----------------------------------------------------------
  \ History of this file
  
  \ 1985, 1986, 1987:
  \
  \ `\`, `INVERT`, `>=`, `<=`, `<>`, `SGN` (from the Abersoft
  \ Forth manual), `NOR`, `NAND`, `NXOR`, `DEPTH`, `PICK`,
  \ `DRAWL`, `RDRAW`, `RDRAWL`.
  \
  \ 2015-03:
  \
  \ `CHAR`, `[CHAR]`, `PARSE-TEXT`, `PARSE-NAME`, `SLIT`, `S,`,
  \ `SLITERAL`, `.(`, `(S)`, `S"`, `S'`, `TLOAD`, `RUNT`,
  \ `BOUNDS`, `NIP`, `[DEFINED]`, `BYE`, `UDG!`, `AKEY`,
  \ `THRU`, `+LOAD` (after Gforth), `+THRU` (after Gforth),
  \ `RDROP`, `R@`, `RDEPTH`, `SGN`, `(after`, `Gforth)`,
  \ `RECURSE`, `ROLL` (after Gforth), `XY>ATTRA`, `[DEFINED]`.
  \
  \ 2015-03-28:
  \
  \ The graphics and strings extensions are moved to their own
  \ files. New: `[UNDEFINED]`. Fixes of Abersoft Forth's bugs:
  \ the RAM disk length and `EXIT`. Fix: `RDROP` was wrong.
  \ New: `/INIT-DISC`, `TUCK`.
  \
  \ 2015-03-29:
  \
  \ `TRUE`, `FALSE`, `ON`, `OFF`, `-ROT`, `?EXIT` (after
  \ Gforth), `ALIAS` (for code words only), `HERE:` (Afera
  \ specific), `BUFFER:` (after Forth-2012). The renamings
  \ (`BYE`, `-DUP`, `VLIST`) are moved to an own file.
  \
  \ 2015-03-30:
  \
  \ `.(` is moved from the strings module, in order to use it
  \ at block headers. New: `MS`.
  \
  \ 2015-03-31:
  \
  \ New: `INKEY?`, `?RSTACK`.
  \
  \ Change: `TLOAD` is combined into `RUNT`.
  \
  \ Fix: Now `RUNT` clears the return stack.
  \
  \ Fix: `+LOAD`: the problem was `BLK` doesn't hold the block
  \ number in Abersoft Forth: it has to be divided by `B/SCR`
  \ (a constant that returns 8). The definitions of `LOAD` and
  \ `-->`, from Don Thomasson's book "Spectrum Advanced Forth",
  \ gave the definitive clue.
  \
  \ Change: simpler definitions of `>=` and `<=`; `NEGATE` is
  \ removed because it already exists, but it's called `MINUS`.
  \
  \ 2015-04-01:
  \ 
  \ Change: Now `RUNT` halts if Break is pressed.  The problem
  \ was the Fuse emulator, when the TAP file is finished,
  \ rewinds it, and this behaviour is not configurable.
  \ Therefore loading a single module of the library was
  \ impossible: it loaded itself in an endless loop.  This
  \ change solves this problem to some extent, while still
  \ keeping `RUNT` for chain loading. A better solution is
  \ searched.
  \
  \ New: Patches that make it possible to use the whole screen.
  \
  \ New: `RND`, adapted from Bertie, the demo program bundled
  \ with Abersoft Forth.
  \
  \ 2015-04-02:
  \
  \ Fix: Now `AT` works on line 32. The solution was to use
  \ `>CHAN` (conversion of `TCH` from Lennart Benschop's
  \ SPECTRUM FORTH83) instead of. `EMIT`.
  \
  \ Improvement: `RUNT` checks `DEPTH` instead of the Break
  \ key.
  \
  \ New: `VALUE`, `TO`, `[TO]` and `<TO>`.
  \
  \ 2015-04-03:
  \
  \ Improvement: `RUNT` checks also `BLK`; this make it
  \ possible to use it manually to load the next RAM disk.
  \
  \ Change: `CHAR` and `[CHAR]` are moved here from the strings
  \ module.
  \
  \ 2015-04-08:
  \
  \ New (moved from the assembler): `2*`, `2-`, 1-`.
  \
  \ 2015-04-09:
  \
  \ New: `/BLOCK`.
  \
  \ Change: `AKEY` and `INKEY?` are moved to their own file
  \ <key.fsb>.
  \
  \ 2015-04-15:
  \
  \ Change: `VALUE` and related words are moved to <value.fsb>;
  \ all words related to whole screen support are moved to
  \ <plusscreen.fsb>; `ALIAS` is moved to <alias.fsb>; `CELL`
  \ and related words are moved to <cell.fsb>; `RUNT` and
  \ `?RUNT` are moved to <runt.fsb>; `HERE:` and `BUFFER:` are
  \ moved to <buffercol.fsb>.
  \
  \ 2015-04-16:
  \
  \ Change: `2*`, `2-`, `1-`, `PICK`, `NIP` and `TUCK` are
  \ rewritten in Z80.
  \
  \ New: `2/`, written in Z80 (still not working fine with
  \ negative numbers).

  \ -----------------------------------------------------------

  \ vim: filetype=abersoftforth
